plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.compose.compiler)
    alias(libs.plugins.google.services)
    alias(libs.plugins.firebase.crashlytics.gradle)
}

android {
    namespace 'co.candyhouse.app'
    compileSdk = rootProject.ext.compileSdkVersion

    signingConfigs {
        release {
            def passStore = System.getenv("storePassword")
            def passKey = System.getenv("keyPassword")
            def ssmKeyAlias = System.getenv("keyAlias")
            storeFile file(rootProject.file('Sesame.jks').getCanonicalFile())
            storePassword "$passStore"
            keyPassword "$passKey"
            keyAlias "$ssmKeyAlias"

            // 处理 CI 环境中的签名文件
            if (System.getenv("CI") == "true") {
                println("CI signing...")
                // CI 环境中跳过签名配置检查，因为签名将由 GitHub Actions 处理
                storeFile = null
                storePassword = null
                keyPassword = null
                keyAlias = null
            }
        }
    }

    defaultConfig {
        def getGitHash = providers.exec {
            commandLine 'git', 'rev-parse', '--short=9', 'HEAD'
        }.standardOutput.asText.get().trim()
        applicationId 'co.candyhouse.sesame2'
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode 266
        versionName '3.0.266'
        vectorDrawables.useSupportLibrary = true
        buildConfigField("String", "GIT_HASH", "\"${getGitHash}\"")
        ndk {
            debugSymbolLevel 'FULL'
        }

        // AWS Cognito 配置
        buildConfigField("String", "AWS_IDENTITY_POOL_ID", "\"${rootProject.ext['aws.cognito.identityPoolId']}\"")
        buildConfigField("String", "AWS_USER_POOL_ID", "\"${rootProject.ext['aws.cognito.userPoolId']}\"")
        buildConfigField("String", "AWS_APP_CLIENT_ID", "\"${rootProject.ext['aws.cognito.appClientId']}\"")

        // Google Maps API Key
        manifestPlaceholders = [
                GOOGLE_MAPS_API_KEY: rootProject.ext['google.maps.apiKey']
        ]
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            firebaseCrashlytics {
                mappingFileUploadEnabled = false
                nativeSymbolUploadEnabled = false
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            if (signingConfigs.release.storeFile != null) {
                println("Local signing...")
                signingConfig signingConfigs.release
            }
            ndk {
                debugSymbolLevel 'FULL'
            }
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }

        ci {
            initWith release
            matchingFallbacks = ['release']
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
        buildConfig true
        compose true
    }
}

androidComponents {
    onVariants(selector().withBuildType("ci")) { variant ->
        if (System.getenv("CI") == "true") {
            def versionCode = System.getenv("VERSION_CODE")
            if (versionCode != null) {
                variant.outputs.forEach { output ->
                    output.versionCode.set(Integer.parseInt(versionCode))
                    println "Set CI version: ${versionCode}"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation project(':sesame-sdk')

    implementation libs.androidx.work.runtime
    implementation libs.androidx.core.ktx.v180
    implementation libs.androidx.preference.ktx
    implementation libs.androidx.swiperefreshlayout

    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.material3
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.compose.ui.viewbinding
    debugImplementation libs.androidx.ui.tooling

    implementation libs.core
    implementation libs.easypermissions
    implementation libs.material
    implementation libs.dfu
    implementation libs.wheelpicker
    implementation libs.indicatorseekbar
    implementation libs.loopview
    implementation libs.glide

    implementation libs.play.services.location
    implementation libs.play.services.maps

    implementation platform(libs.firebase.bom)
    implementation libs.firebase.config.ktx
    implementation libs.firebase.analytics.ktx
    implementation libs.firebase.messaging.ktx
    implementation libs.firebase.crashlytics.ktx
}